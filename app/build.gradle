apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.alexeymerov.redittesttask"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        vectorDrawables.useSupportLibrary = true
        resConfigs "en", "ru"
    }
    buildTypes {

        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildOutputApkName(applicationVariants, "release")
        }
        debug {
            // test before release for shrink apk size
//            debuggable false
//            jniDebuggable false
//            renderscriptDebuggable false
//            pseudoLocalesEnabled false
//            zipAlignEnabled true

            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildOutputApkName(applicationVariants, "debug")
//            ndk { //test before release for shrink apk size
//                abiFilters "arm64-v8a", "armeabi-v7a"
//            }
        }
    }

    lintOptions {
        lintConfig file("lint.xml")
        warningsAsErrors = true
        baseline file("baseline-lint.xml")
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

def buildOutputApkName(DomainObjectSet domainObjectSet, String buildType) {
    domainObjectSet.all { variant ->
        variant.outputs.all { output ->
            outputFileName = "RedditPosts_${buildType}_${new Date().format('dd_MM_yy')}.apk"
        }
    }
}

ext {
    version_support = '27.1.1'
    version_retrofit = '2.3.0'
    version_lifecycle = '1.1.1'
    version_room = '1.1.1'
    version_koin = '0.9.1'
    version_anko = '0.10.4'
    version_glide = '4.8.0'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // kotlin coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.22.2'

    // support
    implementation "com.android.support:design:${version_support}"
    implementation "com.android.support:recyclerview-v7:${version_support}"
    implementation "com.android.support:cardview-v7:${version_support}"
    implementation "com.android.support:support-v13:${version_support}"
    implementation "com.android.support:customtabs:${version_support}"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'

    // retrofit
    implementation "com.squareup.retrofit2:retrofit:${version_retrofit}"
    implementation "com.squareup.retrofit2:converter-moshi:${version_retrofit}"
    implementation "com.squareup.retrofit2:adapter-rxjava2:${version_retrofit}"

    // logging
    implementation 'com.facebook.stetho:stetho:1.5.0'
    implementation 'com.facebook.stetho:stetho-okhttp3:1.5.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.8.1'

    // rx
    implementation 'io.reactivex.rxjava2:rxkotlin:2.0.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.9'

    // koin
    implementation "org.koin:koin-core:${version_koin}"
    implementation "org.koin:koin-android:${version_koin}"
    implementation "org.koin:koin-android-architecture:${version_koin}"

    // ViewModel and LiveData
    implementation "android.arch.lifecycle:extensions:${version_lifecycle}"
    implementation "android.arch.lifecycle:common-java8:${version_lifecycle}"

    // Room
    implementation "android.arch.persistence.room:runtime:${version_room}"
    annotationProcessor "android.arch.persistence.room:compiler:${version_room}"
    kapt "android.arch.persistence.room:compiler:${version_room}"

    // permissions with dsl
    implementation 'com.github.fondesa:kpermissions:1.0.0'

    // anko
    implementation "org.jetbrains.anko:anko-commons:$version_anko"

    // gson
    implementation 'com.google.code.gson:gson:2.8.5'

    //image loader
    implementation "com.github.bumptech.glide:glide:${version_glide}"
    annotationProcessor "com.github.bumptech.glide:compiler:${version_glide}"

}

kotlin {
    experimental {
        coroutines "enable"
    }
}

androidExtensions {
    experimental = true
}
